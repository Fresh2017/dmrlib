import os
import sys

Import('env')

localenv = env.Clone()
localenv.Append(
    LIBS=[
        env['libdmrfec_name'],
        'mbe',
    ],
    LIBPATH=[
        '#build/libdmrfec',
    ],
)

if sys.platform in ('linux2', 'darwin'):
    env['libdmr_name'] = 'dmr'
    localenv.Append(
        LIBS=[
            'pthread',
        ],
    )

if sys.platform == 'darwin':
    # Support Homebrew, http://brew.sh/
    localenv.Append(
        CPPPATH=[
            '#support/darwin/mbelib/include',
            '/usr/local/include',
        ],
        LIBPATH=[
            '#support/darwin/mbelib/lib',
            '/usr/local/lib',
        ],
    )

if sys.platform == 'win32':
    env['libdmr_name'] = 'dmr'
    localenv.Append(
        CPPDEFINES=[
            '_CRT_NONSTDC_NO_DEPRECATE',
        ],
        CPPPATH=[
            '#support/windows/mbelib/include',
        ],
        LIBPATH=[
            '#support/windows/mbelib/lib',
        ],
        LIBS=[
            'ws2_32',
        ],
        LINKFLAGS=[
            '-static-libgcc',
        ],
    )

src = [
    Glob('*.c'),
    Glob('*/*.c'),
    Glob('support/tinycthread/source/*.c'),
]
dmr = localenv.SharedLibrary(env['libdmr_name'], src)
#env.StaticLibrary(name, src)
Return('dmr')
