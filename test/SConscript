import os
import subprocess
import sys

from SCons.Script.SConscript import SConsEnvironment

Import('env')

testenv = env.Clone()
testenv.Append(
    LIBS=[
        env['libdmr_name'],
    ],
    LIBPATH=[
        '#build/libdmr',
    ],
)

if sys.platform in ('linux', 'linux2'):
    testenv.AppendENVPath(
        'LD_LIBRARY_PATH', 'dist',
    )

tests = {}
alias = {}
for test in Glob('test*.c'):
    #print('test', test.abspath, dir(test))
    name = os.path.splitext(os.path.basename(test.abspath))[0]
    tests[name] = testenv.Program(name, [test.abspath])[0]
    alias[name] = Alias(name, [tests[name]], tests[name].path)
    run = testenv.Command(
        target=name + '_run',
        source=tests[name].abspath,
        action=tests[name].abspath,
    )
    Depends(run, tests[name])
    AlwaysBuild(run)
    Default(run)

test_programs = tests.values()
Return('test_programs')
